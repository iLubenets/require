plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

archivesBaseName = 'require'
group = 'com.github.ilubenets'
version = project.hasProperty('releaseVersion') ? releaseVersion : '0.1-local'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    // test
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId group
            artifactId archivesBaseName
            version version
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Require assertions'
                description = 'Lightweight assertion library for different java types and structures checks with predefined human friendly messages'
                url = 'https://github.com/iLubenets/require/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ilubenets'
                        name = 'Igor Lubenets'
                        email = 'ilubenets@gmail.com'
                    }
                }
                scm {
                    url = 'https://github.com/iLubenets/require/'
                    connection = 'scm:https://github.com/iLubenets/require.git'
                    developerConnection = 'scm:ssh:git@github.com:iLubenets/require.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications
}
