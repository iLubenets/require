plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.22.0'
}

group = 'com.github.ilubenets'
archivesBaseName = 'require'
version = '1.3'
sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.findbugs:jsr305:3.0.2'
    // test
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testCompile 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testCompile 'org.junit.jupiter:junit-jupiter-params:5.7.0'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Require assertions'
                description 'Lightweight assertion library for different java types and structures checks with predefined human friendly messages'
                url 'https://github.com/iLubenets/require/'
                packaging 'jar'
                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'ilubenets'
                        name 'Igor Lubenets'
                        email 'ilubenets@gmail.com'
                    }
                }
                scm {
                    url 'https://github.com/iLubenets/require/'
                    connection 'scm:https://github.com/iLubenets/require.git'
                    developerConnection 'scm:ssh:git@github.com:iLubenets/require.git'
                }
            }
        }
    }
}

nexusStaging {
    packageGroup = project.getGroup()
    stagingProfileId = ossrhUsername
}
